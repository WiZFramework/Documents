<!DOCTYPE html>
<html lang="ja">
<head>
<meta charset="utf-8">
<link href="css/main.css" rel="stylesheet" type="text/css">
<title>言語を作ろうプロジェクト（2019年）</title>
</head>
<body>
<!-- 全体コンテナ　-->
<div id="container">
<div id="header">
<h3>０３．トレース言語とインタプリタ</h3>
</div>
<!-- コンテンツ　-->
<div id="contents">
<h3>０３－０５．コメントの実装</h3>
　この項では、<b>コメント</b>を実装します。<br />
　<b>コメント</b>と単純に言いますが、前項までの考え方とは、全く違う考え方を導入しなければいけません。<br />
　そのため、実装にあたり<b>oreProject.l</b>および<b>oreProject.y</b>を全面的に書き換えます。<br />
　まず、<b>oreProject.l</b>です。
<h4>oreProject.lの書き換え</h4>
<div class="box1">
<pre>
%{
#include &lt;stdio.h>
#include "oreProject.tab.h"
#define YY_SKIP_YYWRAP 1
int gLine = 1;
int yywrap(void){ return 1; }

%}
%s COMMENT
%s LINE_COMMENT
%%
&lt;INITIAL>{
    ";"     return SEMICOLON;
    "+"     return ADD;
    "-"     return SUB;
    "*"     return MUL;
    "/"     return DIV;
    "p"     return PRINT;
    "/*"    { BEGIN(COMMENT);}
    [/][/]+ { BEGIN(LINE_COMMENT);}
    [1-9][0-9]* {
        double temp;
        sscanf(yytext, "%lf", &temp);
        yylval.double_value = temp;
        return DOUBLE_LITERAL;
    }
    [0-9]*\.[0-9]* {
        double temp;
        sscanf(yytext, "%lf", &temp);
        yylval.double_value = temp;
        return DOUBLE_LITERAL;
    }
    "\n"    {gLine++;}
    [ \t]   ;
    .   { return 0;}
}
&lt;COMMENT>{
    "\n"    {gLine++;}
    "*/" {BEGIN(INITIAL);}
    .   ;
}
&lt;LINE_COMMENT>{
    "\n" { gLine++; BEGIN(INITIAL);}
    .   ;
}
%%
</pre>
</div>
　続いて<b>oreProject.y</b>の書き換えです。<br />
<h4>oreProject.yの書き換え</h4>
<div class="box1">
<pre>
%{
#include &lt;stdio.h>
#include &lt;stdlib.h>
#include "../oreProject/proc.h"
#define YYDEBUG 1
extern int yylex(void);
int yyerror(char const *str){
    extern char *yytext;
    extern int gLine;
    fprintf(stderr,"%s, line: %d, near %s\n",str, gLine, yytext);
    return 0;
}
%}
%union {
    int     int_value;
    double  double_value;
}
%token &lt;double_value>    DOUBLE_LITERAL
%token SEMICOLON ADD SUB MUL DIV CR PRINT
%type &lt;double_value> expression term primary_expression
%%
statement_list
    : statement
    | statement_list statement
    ;
statement
    : SEMICOLON
    | expression SEMICOLON
    {
        ore::procEmpty();
    }
    | PRINT expression SEMICOLON
    {
        ore::procPrint($2);
    }
expression
    : term
    | expression ADD term
    {
        $$ = ore::procAdd($1,$3);
    }
    | expression SUB term
    {
        $$ = ore::procSub($1,$3);
    }
    ;
term
    : primary_expression
    | term MUL primary_expression 
    {
        $$ = ore::procMul($1,$3);
    }
    | term DIV primary_expression
    {
        $$ = ore::procDiv($1,$3);
    }
    ;
primary_expression
    : DOUBLE_LITERAL
    ;
%%
</pre>
</div>
　これで<b>リビルド</b>して、<b>index.ore</b>を以下のように書き換え、保存しましょう。<br />
<div class="box1">
<pre>
p 10 - 3;
p 10 / 3;
p 30 * 45
/*
aaabbb  
////aaaa

*/

+ 112 * 5;

////  sssss
;
p 45 + 23 * 15
; 4 - 23 / 15;

</pre>
</div>
　実行してみると、以下のような出力が得られます。
<div class="box1">
<pre>
7
3.33333
1910
390

.../oreProject\Debug\oreProject.exe (プロセス 10184) は、コード 0 を伴って終了しました。
このウィンドウを閉じるには、任意のキーを押してください . . .
</pre>
</div>
<h4>oreProject.lの説明</h4>
　まず<b>oreProject.l</b>から説明します。<br />
　<b> .l </b>は<b>Flex</b>の拡張子です。<b>Flex</b>は<b>字句解析</b>を始める際に<b>ステート</b>というのを持っています。<br />
　<b>ステート</b>は<b>状態</b>です。最初は<b>&lt;INITIAL></b>という状態で始まります。
<div class="box1">
<pre>
%%
&lt;INITIAL>{
    ";"     return SEMICOLON;
    "+"     return ADD;
    "-"     return SUB;
    "*"     return MUL;
    "/"     return DIV;
    "p"     return PRINT;
    "/*"    { BEGIN(COMMENT);}
    [/][/]+ { BEGIN(LINE_COMMENT);}
...
</pre>
</div>
　のような記述は<b>&lt;INITIAL></b>状態の記述です。<br />
　状態は、ユーザーが定義することができます。
<div class="box1">
<pre>
%s COMMENT
%s LINE_COMMENT
</pre>
</div>
　のようにします。<br />
　<b>&lt;INITIAL></b>状態のときに<b>"/*"</b>が見つかれば
<div class="box1">
<pre>
    "/*"    { BEGIN(COMMENT);}
</pre>
</div>
　のように<b>&lt;COMMENT></b>に移行します。<b>BEGIN(ステート)</b>でそのステートの状態になります。<br />
　<b>&lt;COMMENT></b>状態にいるときは
<div class="box1">
<pre>
&lt;COMMENT>{
    "\n"    {gLine++;}
    "*/" {BEGIN(INITIAL);}
    .   ;
}
</pre>
</div>
　のように<b>"*/"</b>に出会うと、<b>&lt;INITIAL></b>状態に戻ります。<br />
　<b>&lt;COMMENT></b>状態では<b>"\n"</b>がある都度、<b>gLine</b>をインクリメントします。<br />
　<b>gLine</b>はプログラム内の行数です。
<div class="box1">
<pre>
    .   ;
</pre>
</div>
　というのは<b>そのほかは無視</b>という意味です<b> . （ドット）</b>は<b>そこまで指定のなかったトークン以外</b>という意味です。<br />
<br />
　さて<b>oreProject.l</b>にはもう一つ、<b>&lt;LINE_COMMENT></b>という状態があります。<b>1行コメント</b>です。<br />
　<b>1行コメントかどうか</b>の判断は、<b>&lt;INITIAL></b>状態において
<div class="box1">
<pre>
    [/][/]+ { BEGIN(LINE_COMMENT);}
</pre>
</div>
　という正規表現によって振り分けます。<br />
　<b>[/][/]+</b>というのは<b>1文字目が/で、2文字以降も/が1個以上続く場合</b>という意味です。<br />
　ですから
<div class="box1">
<pre>
//　コメント
///　コメント
///////　コメント
</pre>
</div>
　のいずれの記述でもコメントになります。<br />
　<b>&lt;LINE_COMMENT></b>からの状態の移行は
<div class="box1">
<pre>
&lt;LINE_COMMENT>{
    "\n" { gLine++; BEGIN(INITIAL);}
    .   ;
}
</pre>
</div>
　のように<b>改行</b>に出会うと<b>&lt;INITIAL></b>状態に戻ります。<br />
　ではコメント同士が交差した場合はどうなるでしょうか
<div class="box1">
<pre>
/*
コメント1
//  コメント2

*/
</pre>
</div>
　このコメントは両方とも有効です。<b>1行コメント</b>は<b>/* */</b>のコメント内にあるので、どのみち<b>無視</b>されるので問題ありません。<br />
　しかし
<div class="box1">
<pre>
// コメント1 /*

コメント2

*/
</pre>
</div>
　この場合は<b>コメント2</b>は無効（コメントにならない）です。コメントの始まり<b>/*</b>は<b>1行コメント</b>の中にあり、無視されるために、<b>*/</b>が、<b>&lt;INITIAL></b>状態でいきなり現れる形になります。<br />
　<b>Flex</b>は<b>掛け算と割り算</b>と解析するかもしれませんが、その前後に式はないのと、<b>*/</b>という演算もないので、エラーになります。<br />
<br />
　<b>oreProject.l</b>では、文の終了をあらわす<b> ; </b>を定義しています。
<div class="box1">
<pre>
    ";"     return SEMICOLON;
</pre>
</div>
　の部分です。このトークンの用法は<b>oreProject.y</b>で記述します。
<h4>oreProject.yの説明</h4>
　構文解析の<b>Bison</b>の定義は<b>oreProject.y</b>に記述します。<br />
　ここでは<b>コメント</b>については記述しません。（<b>oreProject.l</b>で終わってます。）<br />
　まず、シンタックスエラーの表示ですが
<div class="box1">
<pre>
int yyerror(char const *str){
    extern char *yytext;
    extern int gLine;
    fprintf(stderr,"%s, line: %d, near %s\n",str, gLine, yytext);
    return 0;
}
</pre>
</div>
　のように、少し詳しくしました。何行目でエラーが出てるかわかるようにしています。<br />
　続いて
<div class="box1">
<pre>
%token <span class="red">SEMICOLON</span> ADD SUB MUL DIV CR PRINT
</pre>
</div>
　のように、<b>SEMICOLON</b>を新しいトークンに加えています。<br />
　また
<div class="box1">
<pre>
statement_list
    : statement
    | statement_list statement
    ;
statement
    : SEMICOLON
    | expression SEMICOLON
    {
        ore::procEmpty();
    }
    | PRINT expression SEMICOLON
    {
        ore::procPrint($2);
    }
</pre>
</div>
　のように、前は<b>line_list</b>および<b>line</b>となっていたのを<b>statement_list</b>および<b>statement</b>と変えました。<b>行</b>という概念がなくなったからです。<br />
　<b>statement</b>は
<div class="box1">
<pre>
statement
    : SEMICOLON
</pre>
</div>
　とあるように
<div class="box1">
<pre>
 ;
</pre>
</div>
　だけの、いわゆる<b>空文</b>も<b>ステートメント</b>になります。<br />
　続く
<div class="box1">
<pre>
    | expression SEMICOLON
    {
        ore::procEmpty();
    }
</pre>
</div>
　のように
<div class="box1">
<pre>
10 + 20;
</pre>
</div>
　のような記述も、計算はしますが、結果が何らかの影響しない文も定義してます。それらは
<div class="box1">
<pre>
        ore::procEmpty();
</pre>
</div>
　と何もしない関数を呼んでいます。<br />
　このあたりは将来変わる可能性はあります。<br />
　続く
<div class="box1">
<pre>
    | PRINT expression SEMICOLON
</pre>
</div>
　は意味があります。
<div class="box1">
<pre>
p 10 + 20;
</pre>
</div>
　のような記述です。これは<b> p </b>命令があるので、出力します。具体的には
<div class="box1">
<pre>
    | PRINT expression SEMICOLON
    {
        <span class="red">ore::procPrint($2);</span>
    }
</pre>
</div>
　と出力関数を呼んでいます。<br/>
　他の記述は以前のものと変わりません。
<h4>まとめ</h4>
　この項では<b>コメント</b>と<b>ステートメント</b>について、定義しました。<br />
　今後もいろんな<b>ステートメント</b>が追加されます。<br />
　次項は<b>変数</b>を定義しましょう。
</div>
<!-- /コンテンツ　-->
<hr>
<!-- フッタ　-->
<ul class="foot_navi">
<li><a href="03_04.html">前ページ</a></li>
<li><a href="index.html">目次</a></li>
<li><a href="03_06.html">次ページ</a></li>
</ul>
<!-- /フッタ　-->


</div>
<!-- /全体コンテナ　-->
</body>
</html>
