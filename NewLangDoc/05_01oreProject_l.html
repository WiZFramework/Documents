<!DOCTYPE html>
<html lang="ja">
<head>
<meta charset="utf-8">
<link href="css/main.css" rel="stylesheet" type="text/css">
<title>言語を作ろうプロジェクト（2019年）</title>
</head>
<body>
<!-- 全体コンテナ　-->
<div id="container">
<div id="header">
<h3>０５．ここまでの全ソース</h3>
</div>
<!-- コンテンツ　-->
<div id="contents">
<h3>oreProject.l</h3>
<div class="box1">
<pre>
%{
#include &lt;stdio.h>
#include "oreProject.tab.h"
#include "../oreProject/proc.h"
#define YY_SKIP_YYWRAP 1
int gLine = 1;
int yywrap(void){ return 1; }
%}
%s COMMENT
%s LINE_COMMENT
%s STR_LITERAL_ST_D
%s STR_LITERAL_ST_S
%%
&lt;INITIAL>{
    "tru"   return TRUE_T;
    "true"   return TRUE_T;
    "fls"   return FALSE_T;
    "false"   return FALSE_T;
    "if"    return IF;
    "els"   return ELSE;
    "else"   return ELSE;
    "sch"   return SWITCH;
    "switch"   return SWITCH;
    "cse"   return CASE;
    "case"  return CASE;
    "def"   return DEFAULT;
    "default"   return DEFAULT;
    "whl"   return WHILE;
    "while"   return WHILE;
    "for"   return FOR;
    "feh"   return FOREACH;
    "as"   return AS;
    "foreach"   return FOREACH;
    "ctn"   return CONTINUE;
    "continue"   return CONTINUE;
    "brk"   return BREAK;
    "break"   return BREAK;
    "fnc"   return FUNCTION;
    "function"   return FUNCTION;
    "ret"   return RETURN;
    "return"   return RETURN;
    "("     return LP;
    ")"     return RP;
    "{"     return LC;
    "}"     return RC;
    "["     return LB;
    "]"     return RB;
    ":"     return COLON;
    ";"     return SEMICOLON;
    ","     return COMMA;
    "*="    return MULASS;
    "/="    return DIVASS;
    "+="    return ADDASS;
    "-="    return SUBASS;
    "&="    return AMPASS;
    "++"    return INCREMENT;
    "--"    return DECREMENT;
    "=="    return EQ;
    "!="    return NE;
    "&lt;="    return LE;
    ">="    return GE;
    "&lt;"      return LT;
    ">"     return GT;
    "&&"    return LOGICAL_AND;
    "||"    return LOGICAL_OR;
    "+"     return ADD;
    "-"     return SUB;
    "*"     return MUL;
    "/"     return DIV;
    "%"     return MOD;
    "&"     return AMP;
    "="     return ASS;
    "ary"   return ARRAY;
    "array"   return ARRAY;
    "setcnf"   return SETCONFIG;
    "getcnf"   return GETCONFIG;
    "pn"    return PRINTN;
    "p"     return PRINT;
    "/*"   { BEGIN(COMMENT);}
    [/][/]+ { BEGIN(LINE_COMMENT);}
    "\""    {
                ore::Interpreter::getInp()->startStringLiteral();
                BEGIN(STR_LITERAL_ST_D);
            }
    "\'"    {
                ore::Interpreter::getInp()->startStringLiteral();
                BEGIN(STR_LITERAL_ST_S);
            }
    [0-9][0-9]* {
        yylval.expression = ore::Interpreter::getInp()->createIntLiteralExp(yytext);
        return INT_LITERAL;
    }
    [0-9]*\.[0-9]* {
        yylval.expression = ore::Interpreter::getInp()->createDoubleLiteralExp(yytext);
        return DOUBLE_LITERAL;
    }
    "\$"[A-Za-z_][A-Za-z_0-9]* {
        yylval.fixedString = ore::Interpreter::getInp()->createFixedString(yytext);
        return LOCAL_IDENTIFIER;
    }
    [A-Za-z_][A-Za-z_0-9]* {
        yylval.fixedString = ore::Interpreter::getInp()->createFixedString(yytext);
        return IDENTIFIER;
    }
    "\n"    { gLine++; }
    [ \t]   ;
    .       { return 0; }
}
&lt;COMMENT>{
    "\n"    { gLine++;}
    "*/"    { BEGIN(INITIAL); }
    .       ;
}
&lt;LINE_COMMENT>{
    "\n"    { gLine++; BEGIN(INITIAL); }
    .       ;
}
&lt;STR_LITERAL_ST_D>{
    "\n"    { gLine++; ore::Interpreter::getInp()->addStringLiteral('\n'); }
    "\""    { BEGIN(INITIAL); return STR_LITERAL; }
    .       { ore::Interpreter::getInp()->addStringLiteral(yytext[0]); }
}
&lt;STR_LITERAL_ST_S>{
    "\n"    { gLine++; ore::Interpreter::getInp()->addStringLiteral('\n'); }
    "\'"    { BEGIN(INITIAL); return STR_LITERAL; }
    .       { ore::Interpreter::getInp()->addStringLiteral(yytext[0]); }
}
%%
</pre>
</div>

</div>
<!-- /コンテンツ　-->
<hr>
<!-- フッタ　-->
<ul class="foot_navi">
<li><a href="05_01.html">戻る</a></li>
</ul>
<!-- /フッタ　-->
</div>
<!-- /全体コンテナ　-->
</body>
</html>
